<!--
 * @Descripttion: 
 * @Author: lizhengxing
 * @Date: 2022-10-02 11:36:46
 * @LastEditTime: 2022-10-12 19:53:29
-->
## 类型

#### 1、值类型

> 因为这些类型的变量始终按值来传递，当这些变量被用作函数参数或者用在赋值语句中时，总会进行值拷贝。

##### 1-1、布尔类型

`bool` 取值为字面常量 `true` 和 `false`。默认为 `false`。

运算符：

- `!` 逻辑非
- `&&` 逻辑与（短路）
- `||` 逻辑或（短路）
- `==` 等于
- `!=` 不等于

##### 1-2、整型

> [参考文章](https://toutiao.io/posts/kwbfvk/preview)

`int`/`uint`：分别表示有符号和无符号的不同位数的整型变量。支持 `int8` ~ `int256` 以及 `uint8` ~ `uint256`，以8位为步长递增（从8 bit (1 byte) 到 256bit (32 bytes) ）。`uint` 和 `int` 分别是 `uint256` 和 `int256` 的别名。初始值都为 `0`.

`int` 范围是 `-2^(n - 1) ~ 2^(n - 1) - 1`，比如 `int8` 范围就是 `-128 ~ 127`

`uint` 范围是 `0 ~ 2^n - 1`，比如 `uint8` 范围就是 `0 ~ 255`

运算符：

- 比较运算符： `<=`、`<`、`==`、`>`、`>=`、`!=` 返回布尔值
- 位运算符：`&`、`|`、`^` （异或）、`~` （位取反）

  > 在数字的二进制补码表示上执行，这意味着 `~int256 (0) == int256 (-1)`
  > & : 相同位，两个都为1才是1，只要有一个是0就是0
  > | : 相同位，两个有一个1就是是1，都是是0就是0
  > ^ : 相同位，不相等为1，相等为0
  > ～ : 0变为1，1变为0

- 移位运算符：`<<` （左移位）、`>>` （右移位）

  > 结果总是被截断的
  > `x << y` 相当于 `x * 2 ** y`
  > `x >> y` 相当于 `x / 2 ** y`，四舍五入到负无穷
  ```solidity
  int a = -1 << 2; // a = -1 * 2 ** 2 = -1 * 4 = -4
  int b = -1 >> 2; // b = -1 / 2 ** 2 = -1 / 4 = 0 （除法截断）
  ```

- 算数运算符：`+` 、`—` 、一元运算符 `—`（针对`int`）、`*`、 `/`、`%`（取余）、** （幂）

  > 取余愿算结果与左操作数的符号相同
  > 幂运算仅适用于无符号类型 `0**0 == 1`

溢出：

- 对于 `int`

```solidity
function intOverflow() public pure returns (int8, int8, int8) {
  int8 a = 127;
  return (a, a + 1, a*a);
}
// 输出为
0: int8: 127
1: int8: -128
2: int8: 1
```
- 对于 `uint`

```solidity
function uintOverflow() public pure returns (uint8, uint8, uint8) {
  uint8 a = 255;
  return (a, a + 1, a*a);
}
// 输出为
0: uint8: 255
1: uint8: 0
2: uint8: 1
```

除法截断：

> 整数除法总是产生整数，分数会取0，除以0会发生错误

```solidity
function litOverflow() public pure returns(int256, int256){
  int a = 1;
  int b = 4;
  return ((a/b)*100, (1/4)*100);
}
// 输出为
0: int256: 0
1: int256: 25
```

##### 1-3、定长浮点型

> solidity 还没有完全支持定长浮点型。可以声明该类型变量，但不能给他们赋值或把他们赋值给其他变量。

`fixed` / `ufixed`: 表示各种大小的有符号和无符号的定长浮点型。在关键字 `ufixedMxN` 和 `fixedMxN` 中， `M` 表示该类型占用的位数（包括小数位和整数位），必须能整除8，即8～256，`N`表示可用的小数位数，0～80之间的任何数。`fixed` 和 `ufixed` 分别是 `fixed128x19` 和 `ufixed128x19` 的别称。

> 它与浮点型最大的不同点在于：浮点型的整数和消暑不分需要的位数是灵活可变的，而它的整数和小数部分的长度收到严格的规定。

运算符：

- 比较运算符： `<=`、`<`、`==`、`>`、`>=`、`!=` 返回布尔值
- 算数运算符：`+` 、`—` 、一元运算符 `—`、一元运算符 `+`、`*`、 `/`、`%`（取余）

##### 1-4、地址类型

- `address`：保存一个20字节的值（以太坊地址的大小）
- `address payable`：可支付地址，与 `address` 相同，不过有成员函数 `transfer` 和 `send`。可以向其发送以太币。

> 允许从 `address payable` 到 `address` 的隐式转换，而从 `address` 到 `address payable` 必须显式转换，通过 `payable(<address>)` 进行转换。


`address` 允许和 `uint160`、整型字面常量、`bytes20`及合约类型进行转换。

只能通过`payable(<address>)`把 `address`类型和合约类型转换为`address payable`。只有能接受以太币的合约类型，才能进行此转换。例如合约要么有 `receive` 或可支付的回退函数。

> payable(0) 是有效的，这是此规则的例外。

运算符： `<=`、`<`、`==`、`>`、`>=`、`!=`

> 如果将使用较大字节数组类型转换为`address`，那么`address`将被截断。

###### 地址类型成员变量

- `balance` 和 `transfer` 成员

可以使用`balance`属性来查询一个地址的余额，也可以使用`transfer`函数向一个可支付的地址发送以太币（以wei为单位）。

如果当前合约余额不足，则`transfer`函数会执行失败，或者如果一台转移被接受账户拒绝，`transfer`函数同样会失败而进行回退。

> \<address\>.balance：以`wei`为单位的地址余额，类型为 `uin256`
> \<address payable\>.transfer(uint256 amount)：向地址发送`amount`数量的`wei`，失败时抛出异常，使用固定的2300`gas`矿工费。

- `send` 成员

`send`是`transfer`的低级版本。如果执行失败，当前合约不会因为异常而终止，会返回`false`。

> \<address payable\>.transfer(uint256 amount) returns (bool)：向地址发送`amount`数量的`wei`，失败时返回`false`，使用固定的2300`gas`矿工费。

- `call` 、 `delegatecall` 和 `staticcall`

> \<address\>.call(bytes memory) returns (bool, bytes memory)：用给定的有效载荷（payload）发出低级 CALL 调用，返回成功状态及返回数据，发送所有可用 gas，也可以调节 gas。支持 value 选项。
> \<address\>.delegatecall(bytes memory) returns (bool, bytes memory)：用给定的有效载荷 发出低级 DELEGATECALL 调用 ，返回成功状态并返回数据，发送所有可用 gas，也可以调节 gas。
> \<address\>.staticcall(bytes memory) returns (bool, bytes memory)：用给定的有效载荷 发出低级 STATICCALL 调用 ，返回成功状态并返回数据，发送所有可用 gas，也可以调节 gas。

```solidity
// 目前不懂
address(nameReg).call{gas: 1000000, value: 1 ether}(abi.encodeWithSignature("register(string)", "MyName"));
```

- `code` 和 `codehash` 成员

> \<address\>.code：返回 `bytes memory`，值可能为空。
> \<address\>.codehash：返回该代码的keccak-256哈希值，为`bytes32`。
> `addr.codehash`比`keccak256(addr.code)`更便宜
> 所有合约都可以转换为 `address`类型，所以可以使用`address(this).balance`查询当前合约的余额。

##### 1-5、合约类型

每一个`contract`定义都有它自己的类型。合约可以显式转换为`address`类型。

只有当合约具有接收 `receive` 函数或 `payable`回退函数时，才能显式和 `address payable`类型相互转换，转换使用`address(x)`执行，如果合约类型没有接收或 `payable`回退功能，则可以使用 `payable(address(x))`转换为`address payable`类型。

合约不支持任何运算符。合约类型的成员是合约的外部函数及public的状态变量。

##### 1-6、定长字节数组

关键字有 `bytes1` `bytes2` ... `bytes32`

运算符：

- 比较运算符： `<=`、`<`、`==`、`>`、`>=`、`!=` 返回布尔值
- 位运算符：`&`、`|`、`^` （异或）、`~` （位取反）
- 移位运算符：`<<` （左移位）、`>>` （右移位）
- 索引访问：如果 `x` 是 `bytesI` 类型，那么 `x[k]` (其中 `0 <= k < I`) 返回第 `k` 个字节（只读）
- `.length` 表示这个字节数组的长度，不能修改长度

```solidity
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

contract BytesArray {
  bytes1 num1 = 0x7a; // 十六进制每一位代表二进制四位 0111 1010
  bytes2 num2 = 0x7a68; // 十六进制每一位代表二进制四位 0111 1010 0110 1000

  function getLength() public view returns (uint8, uint8) {
    return (num1.length, num2.length); // 返回字节的长度，分别为1和2
  }

  function getEl() public view returns (bytes1, bytes1, bytes1) {
    // num1[1] 会报错，访问元素超过字节长度
    return (num1[0], num2[0], num2[1]); // 0x7a 0x7a 0x68
  }
}
```

##### 1-7、变长字节数组

- `bytes`：变长字节数组，并不是值类型

```solidity
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

contract DynamicByte {
  // name长度为3
  bytes public name = new bytes(3);

  function initName() public {
      // name的值为 0x676869
      name[0] = 0x67;
      name[1] = 0x68;
      name[2] = 0x69;
  }

  function getLength() public view returns(uint256) {
      return name.length; // 3
  }

  function changeLength() public {
      name.push(0xaa); // name长度加1，为4，name的值修改为 0x679969aa
  }

  function changeName() public {
      name[1] = 0x99; // name的值修改为 0x679969
  }
}
```

- `string`：变长UTF-8编码字符串类型，并不是值类型，无法获取长度，也无法通过索引访问

```solidity
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

contract DynamicString {
  string public name = "lizhengxing"; // 0x6c697a68656e6778696e67

  function getLength() public view returns(uint256) {
      // return name.length; // 无法获取length
      return bytes(name).length; // 11，转换成字节可以获取
  }

  function getName() public view returns(bytes memory) {
      return bytes(name); // 0x6c697a68656e6778696e67
  }

  function changeName() public {
      bytes(name)[1] = "u"; // name -> luzhengxing getName => 0x6c757a68656e6778696e67
  }

  // 中文测试

  function getChineseNameLength(string memory _name) public pure returns(uint256) {
      return bytes(_name).length; // 输入李正星 =》 9，一个中文占三个字节
  }

  function getChineseNameName(string memory _name) public pure returns(bytes memory) {
      return bytes(_name); // 输入李正星 =》 0xe69d8ee6ada3e6989f
  }
}
```

##### 1-8、地址字面常量

比如像 `0xA47C311F7A9Ed6Dab0e10a990696631937912db9`（长度为42位） 这样的通过了地址校验和测试的十六进制字面量会作为 `address` 类型。而没有通过校验测试的长度在39～41个数字之间的十六进制字面常量，会产生一个错误，可以在0前面添加（对于整数类型）或在0后面添加（对于`bytesNN`类型），以消除错误。

##### 1-9、有理数和整数字面常量

- 整数字面常量由范围在 `0-9` 的一串数字组成，表现为十进制，例如 `35` `。solidity` 中没有八进制，所以前置`0`无效。
- 十进制小数字面常量带有一个 `.`，后面至少一个数字，例如 `.1` 、`1.2`，没有 `1.0`。
- `MeE`（`M * 10 ** E`）的科学符号，指数必须为整数，底数可以是小数，例如`-2e10` 、 `1.2e23`。
- 为了提高可读性，可以在数字之间添加下划线。例如 `123_123` `0x2eff_abde` `1_2e34_34`。下划线仅允许在两位数之间，且不能连续出现。
- 在数值常量表达式中，计算不会溢出，除法也不会截断。
- 有理数为整数（正整数、0、负整数）和分数的统称 。 正整数和正分数合称为正有理数，负整数和负分数合称为负有理数。 因而有理数集的数可分为正有理数、负有理数和零。
- solidity对每个有理数都有对应的数字字面常量类型。整数字面常量和有理数字面常量都属于数值字面常量类型。除此之外，所有的数值字面常量表达式都属于数值字面常量。
- 数值字面常量表达式只要在非字面常量表达式中使用就会转化为非字面常量类型。

##### 1-10、字符串字面常量及类型

- 字符串字面常量是指由双引号或单引号引起来的字符串。可以分为多个连续部分，例如 `"foo" "bar"` 等效于 `"foobar"`。
- 可以隐式转换为 `bytes1` `bytes2` ... `bytes32`，如果合适的话，还可以转换为 `bytes` 和 `string`。
- 支持的转义字符包含：`\<newline>` （转义实际换行）、`\\` （反斜杠）、`\'` （单引号）、`\"` （双引号）、`\b` （退格）、`\f` （换页）、`\n` （换行）、`\r` （回车）、`\t` （标签tab）、`\v` （垂直标签）、`\xNN` （十六进制转义）、`\uNNN` （unicode转义）
  
```solidity
 "\n\"\'\\abc\
 def"
```

##### 1-11、Unicode字面常量

以关键字unicode为前缀，可以包含任意有效的UTF-8序列，它还支持与转义序列完全相同的字符作为常规字符串文字。

```solidity
string memory a = unicode"hello 😊";
```

##### 1-12、十六进制字面常量

- 以关键字hex为前缀，后面紧跟用单引号或双引号引起来的字符串，例如`hex"001122FF"`。字符串的内容必须是一个十六进制的数字，字面常量的值将是该十六进制数字的二进制表示形式。
- 用空格分开的多个十六进制字面常量被合并为一个字面常量，例如 `hex"00112233" hex"44556677"` 等效于 `hex"0011223344556677"`

##### 1-13、枚举类型

枚举是创建用户定义类型的一种方法。枚举需要至少一个成员，默认是第一个成员，不能多于256个成员。选项从0开始的无符号整数值表示。

使用 `type(NameOfEnum).min` 和 `type(NameOfEnum).max` 可以得到给定枚举的最小值和最大值。

```solidity
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.8;

contract Test {
  // 值为{0,1,2,3}
  enum ActionChoices { GoLeft, GoRight, GoStraight, SitStill }
  ActionChoices choice;
  // defaultChoice 为 2， constant 代表常量
  ActionChoices constant defaultChoice = ActionChoices.GoStraight;

  function setGoStraight() public {
      // choice 为 2 
      choice = ActionChoices.GoStraight;
  }

  // 由于枚举类型不属于 ABI 的一部分，因此对于所有来自 Solidity 外部的调用，
  // getChoice 的签名会自动改成 “getChoice() returns (uint8)”

  function getChoice() public view returns (ActionChoices) {
      return choice; // 2
  }

  function getDefaultChoice() public pure returns (uint) {
      return uint(defaultChoice); // 2
  }

  function getLargestValue() public pure returns (ActionChoices) {
      return type(ActionChoices).max; // 3
  }

  function getSmallestValue() public pure returns (ActionChoices) {
      return type(ActionChoices).min; // 0
  }
}
```

##### 1-14、用户定义的值类型

- 一个用户定义的值类型允许在一个基本的值类型上创建一个零成本的抽象，这类似于一个别名，但有更严格的类型要求。
- 使用 `type C is V` 来定义，其中 `C` 是新的类型， `V`必须是内置的值类型（底层类型）。函数 `C.wrap` 被用来从底层类型转换到自定义类型，函数 `C.unwrap` 被用来从自定义类型转换到底层类型。
- 新类型 `C` 没有任何运算符或绑定成员函数，也不允许于其他类型进行显式和隐式转换。
- 自定义类型的值的数据表示继承自底层类型。

```solidity 
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.8;

// 256位其中包含18位小数的十进制定点类型
type UFixed256x18 is uint256;
// 在 UFixed256x18 上进行操作的库
library FixedMath {
  uint constant multiplier = 10 ** 18;

  // 两个UFixed256x18类型相加，溢出恢复，
  // 依赖于uin256的检查算法
  function add(UFixed256x18 _a, UFixed256x18 _b) internal pure returns (UFixed256x18) {
      // .wrap 从底层类型uint256转换到自定义类型UFixed256x18
      // .unwrap 从自定义类型UFixed256x18转换到底层类型uint256
      return UFixed256x18.wrap(UFixed256x18.unwrap(_a) + UFixed256x18.unwrap(_b));
  }

  // UFixed256x18 和 uint256 相乘
  function mul(UFixed256x18 _a, uint256 _b) internal pure returns (UFixed256x18) {
      return UFixed256x18.wrap(UFixed256x18.unwrap(_a) * _b);
  }

  // 向下取整，返回不超过a的最大整数
  function floor(UFixed256x18 _a) internal pure returns (uint256) {
      return UFixed256x18.unwrap(_a) / multiplier;
  }

  // 将 uint256 转换为相同值的 UFixed256x18类型，整数太大会恢复
  function toUFixed256x18(uint256 _a) internal pure returns (UFixed256x18) {
      return UFixed256x18.wrap(_a * multiplier);
  }
}
```

##### 1-15、函数类型

```solidity
function (<parameter types>) {internal|external} [pure|constant|view|payable] [returns (<return types>)]
```

- internal：(__默认__)内部函数，只能在当前合约内部和继承的合约内部调用
- external：外部函数，由一个地址和一个函数签名组成，可以通过外部函数调用传递或返回

类型转换：

- 函数类型A可以隐式转换为函数类型B当且仅当：他们的参数类型相同，返回类型相同，内部/外部属性相同，并且A的状态可变性比B的状态可变性更具限制性，例如：
  - `pure` 函数可以转换为 `view` 和 `non-payable` 函数
  - `view` 函数可以转换为 `non-payable` 函数
  - `payable` 函数可以转换为 `non-payable` 函数
  > 关于 `payable` 和 `non-payable` 函数：如果一个函数是 `payable`，这意味着它也接受零以太支付，因此他也是 `non-payable` 函数。另一方面，`non-payable` 函数将拒绝发送给他的以太币，所以 `non-payable` 函数不能转换为 `payable` 函数。
- `public` 函数既可以是内部函数，也可以是外部函数。如果想将一个函数当作内部函数调用，就使用 `f` 调用，如果想将其当作外部函数调用，就使用 `this.f` 调用
  
成员方法：

public（或 external）函数都有一下成员：
- .address: 返回函数的合约地址
- .selector：返回ABI函数选择器

**如何使用成员**

```solidity
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.8;

contract test {
  function f() public payable returns (bytes4) {
      assert(this.f.address == address(this));
      return this.f.selector;
  }
  function g() public {
      this.f{gas: 10, value: 1}();
  }
}
```
**如何使用内部函数类型**

```solidity
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.8;

library ArrayUtils {
  // 内部函数可以在内部库函数中使用
  // 因为他们会成为同一代码上下文的一部分
  function map(
      uint256[] memory self, 
      function (uint256) pure returns (uint256) f
  )
      internal
      pure
      returns (uint256[] memory r)
  {
      r = new uint256[](self.length);
      for(uint256 i = 0; i < self.length; i++) {
          r[i] = f(self[i]);
      }
  }

  function reduce(
      uint256[] memory self,
      function (uint256, uint256) pure returns (uint256) f
  )
      internal
      pure
      returns (uint256 r)
  {
      r = self[0];
      for(uint256 i = 0; i < self.length; i++) {
          r = f(r, self[i]);
      }
  }

  function range(uint256 length) internal pure returns (uint256[] memory r) {
      r = new uint256[](length);
      for(uint256 i = 0; i < r.length; i++) {
          r[i] = i;
      }
  }
}

contract Pyramid {
    
  using ArrayUtils for *;

  function pyramid(uint256 _l) public pure returns(uint256) {
      return ArrayUtils.range(_l).map(squre).reduce(sum);
  }
  function squre(uint256 _x) internal pure returns (uint256) {
      return _x * _x;
  }
  function sum(uint256 _x, uint256 _y) internal pure returns (uint256) {
      return _x + _y;
  }
}
```

**如何使用外部函数类型**

```solidity
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.8;

contract Oracle {

  struct Request {
      bytes data;
      function(uint256) external callback;
  }

  Request[] private requests;

  event NewRequest(uint);

  function query(bytes memory data, function(uint256) external callback) public {
      requests.push(Request(data, callback));
      emit NewRequest(requests.length - 1);
  }
  function reply(uint256 requestID, uint256 response) public {
      // 这里检查回复来自可信来源
      requests[requestID].callback(response);
  }
}

contract OracleUser {
  Oracle constant private ORACLE_CONST = Oracle(address(0x00000000219ab540356cBB839Cbe05303d7705Fa));

  uint256 private exchangeRate;

  function buySomething() public {
      ORACLE_CONST.query("USD", this.oracleResponse);
  }

  function oracleResponse(uint256 response) public {
      require(
          msg.sender == address(ORACLE_CONST),
          "Only oracle can call this"
      );
      exchangeRate = response;
  }
}
```

#### 2、引用类型

使用引用类型，必须明确指明数据存储在哪种类型的空间（位置）里。

- `memory` ： 内存，数据仅在其生命周期内（函数调用期间）有效。不能用于外部调用
- `storage` ： 存储，只要合约存在就一直存储
- `calldata` ：调用数据，用来保存函数参数的特殊数据位置，是一个只读位置

> 更改数据位置或类型转换将始终产生自动进行一份拷贝，而在同一数据位置内（对用存储`storage`来说）的复制仅在某些情况下进行拷贝。

##### 2-1、数据位置

所有的引用类型，都有一个额外注解`数据位置`，来说明数据存储位置，包括内存`memory`、存储`storage`和调用数据`calldata`。调用数据`calldata`是不可修改的、非持久的函数参数存储区域，效果大多类似内存`memory`。主要用于外部函数的参数，也可用于其他变量。

> 如果可以的话，尽量使用`calldata`作为数据位置，因为它将避免复制，并确保不能修改数据。

数据位置不仅仅表示数据如何存储，它同样影响着赋值行为：

- 1、在存储和内存之间互相赋值，都会创建一份独立拷贝
- 2、从内存到内存的赋值，只创建引用，这意味着更改内存变量，其他引用相同数据的所有其他内存变量的值也会发生变化
- 3、从存储到本地存储变量的赋值值分配一个引用
- 4、其他向存储的赋值，总是进行拷贝

```solidity
// SPDX-License-Identifier: GPL-3.0

pragma solidity ^0.8.0

contract Tiny {

  uint[] x; // x为storage

  function f(uint[] memory memoryArray) public {
    x = memoryArray; // 第一条，memory -> storage 进行拷贝
    uint[] storage y = x; // storage -> storage 进行引用
    y[7]; // 返回第八个元素
    y.pop(); // 删除最后一个元素，修改y同时修改x
    delete x; // 清除数组，同时修改y
    delete y; // 这会重置指针，不可行
    y = memoryArray; // 不可行
  }
}
```

##### 2-2、数组

数组可以在声明时指定长度，也可以动态调整大小（长度）。

声明方式为：`T[K]`（类型为T，长度为K），`T[]`（类型为T，长度不固定，动态数组），`T[][K]`（长度为K，类型为T的动态数组，二维数组，例如访问第三个动态数组的第七个元素，为`x[2][6]`）

数组元素可以是任何类型，对类型的限制是映射只能在`storage`中，并且公开访问函数的参数需要是`ABI`类型

状态变量标记为`public`的数组，会自动创建一个`getter`函数，数字索引是这个函数的参数

访问超出数组长度的元素会导致异常。可以使用`.push()`方法在末尾追加一个零初始化的新元素并返回对它的引用

###### bytes 和 string 也是数组

`bytes` 类似于 `bytes1[]`，但它在调用数据和内存中会被“紧打包”。更多时候应该使用`bytes`，gas费用更低。使用长度限制的字节数组，应该使用`bytes1`到`bytes32`的具体类型。

`string`不允许用长度或索引来访问，比较两个字符串通过计算他们的`keccak256-hash`来比较：`keccak256(abi.encodePacked(s1)) == keccak256(abi.encodePacked(s2))`，使用 `string.concat(s1,s2)`来拼接两个字符串

> 如果要访问以字节表示的字符串s，可以使用`bytes(s).length / bytes(s)[7] = 'x'`

###### 函数 bytes.concat 和 string.concat

- `string.concat`：连接任意数量的 `string` 字符串。返回一个 `string memory`
- `bytes.concat`：连接任意数量的 `bytes` 或 `bytes1 ... bytes32` 值。返回一个 `bytes memory`。
- 如果不使用参数调用这两个函数，将返回空数组（空字符串和0x）

```solidity
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.12;

contract Concat {
  string s = "Storage";

  function c(bytes calldata bc, string memory sm, bytes2 b)
    public 
    view 
    returns (string memory, bytes memory, uint256, uint256) 
  {
    string memory concatString = string.concat(s, string(bc), sm);
    bytes memory concatBytes = bytes.concat(bytes(s), bc, bytes(sm), b);
    return (concatString, concatBytes, bytes(concatString).length, concatBytes.length);
  }
}
```

###### 数组常量

数组常量是在方括号中（`[...]`）包含一个或多个逗号分隔的表达式，例如`[1, a, f(3)]`。

数组常量的类型通过以下方式确定

- 数组常量总是一个静态大小的内存数组，其长度为表达式的数量。
- 数组的基本类型是第一个表达式的类型，数组的全部元素需要转换成相同类型
- 定长的内存数组不能赋值给变长的内存数组

###### 数组成员

- `length`：当前数组长度
- `push()`：动态的存储数组以及`bytes`类型都有这个成员函数，它添加新的零初始化元素到数组末尾，并返回该元素的引用
- `push(x)`：动态的存储数组以及`bytes`类型都有这个成员函数，它添加新的元素到数组末尾，没有返回值
- `pop)`：动态的存储数组以及`bytes`类型都有这个成员函数，它移除数组末尾元素，同样会在移除的元素上隐含调用 `delete`， 没有返回值

```solidity
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.12;

contract ArrayContract {
  // storage 长度为 2**20 的 uint 数组
  uint256[2**20] public aLotOfIntegers;

  // 该变量为类型是包含2个bool类型定长数组的动态数组
  bool[2][] public pairsOfFlags;

  function setAllFlagPairs(bool[2][] memory newPairs) public {
      // 内存向存储赋值，进行拷贝，newPairs 替换整个 pairsOfFlags
      pairsOfFlags = newPairs;
  }

  // 结构体，包含一个uint数组，一个uint
  struct structType{
      uint256[] contents;
      uint256 moreInfo;
  }
  // 声明一个结构体变量
  structType public s;

  function f(uint256[] memory c) public {
      // 从存储到本地存储变量的赋值分配一个引用
      structType storage g = s;
      // g和s同时改变
      g.moreInfo = 2;
      // 拷贝，因为 g.contents 不是本地变量，他是本地变量的成员
      g.contents = c;
  }

  function setFlagPairs(uint256 index, bool flagA, bool flagB) public {
      // 访问不存在的索引将会引发异常
      // 给pairsOfFlags第index项的第一个赋值为flagA
      pairsOfFlags[index][0] = flagA;
      // 给pairsOfFlags第index项的第二个赋值为flagB
      pairsOfFlags[index][1] = flagB;
  }

  function changeFlagArraySize(uint256 newSize) public {
      // 使用 push 和 pop 是更改数组长度的唯一方法
      if(newSize > pairsOfFlags.length) {
          while(pairsOfFlags.length < newSize)
              pairsOfFlags.push();
      } else if(newSize < pairsOfFlags.length) {
          while(pairsOfFlags.length > newSize)
              pairsOfFlags.pop();
      }
  }

  function clear() public {
      // 完全清除数组
      delete pairsOfFlags;
      delete aLotOfIntegers;
      // 效果和上面相同
      // pairsOfFlags.length = new bool[2][](0)
  }

  bytes public bytesData;

  function byteArrays(bytes memory data) public {
      bytesData = data;
      for(uint256 i = 0; i < 7; i++)
          bytesData.push();
      bytesData[3] = 0x67;
      delete bytesData[2];
  }

  function addFlag(bool[2] memory flag) public returns (uint256) {
      pairsOfFlags.push(flag);
      return pairsOfFlags.length;
  }

  function createMemoryArray(uint256 size) public pure returns(bytes memory) {
      // 使用new创建动态内存数组
      uint256[2][] memory arrayOfPairs = new uint256[2][](size);
      // 內联数组始终是静态大小的，如果使用字面常量，必须至少提高一种类型
      arrayOfPairs[0] = [uint256(1), 2];
      // 创建一个动态字节数组
      bytes memory b = new bytes(size);
      for(uint256 i = 0; i < b.length; i++)
          b[i] = bytes1(uint8(i));
      return b;
  }
}
```

###### 对存储数组元素的悬空引用

悬空引用是指一个指向不再存在的东西的引用，或者是对象被移除而没有更新引用。

```solidity
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.12;

contract c {
  uint256[][] public s;

  function f() public {
      s.push();
      // 保存数组最后一个元素的引用
      uint256[] storage ptr = s[s.length - 1];
      // 移除最后一个元素
      s.pop();
      // 向不再属于数组的元素写入数据
      ptr.push(0x42);
      // 现在添加元素到s不会添加一个空元素，而是数组长度为1，0x42作为其元素
      s.push();
      assert(s[s.length - 1][0] == 0x42);
  }
}
```

`solidity`不允许在存储中声明对值类型的引用。这些明确的悬空引用被限制在嵌套引用类型中。然而，悬空引用也可能在元组赋值中使用复杂表啊是时临时发生。

```solidity
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.12;

contract c {
  uint256[] public s;
  uint256[] public t;

  constructor() {
      s.push();
      t.push();
  }
  function g() internal returns (uint256[] storage) {
      s.pop();
      return t;
  }
  function f() public returns (uint256[] memory) {
      // 先执行 s.push() ，并获得元素 1 的引用
      // 然后，调用 g，pop移除这个引用,左边的元组元素成了悬空引用
      // 赋值仍会发生，会被写入s之外的数据区域
      (s.push(), g()[0]) = (0x42, 0x17);
      // s.push()使用上一个语句的值
      // s的最后一个元素为0x42
      s.push();
      return s;
      // 执行完之后
      // s => 0,0x42
      // t => 0x17
  }
}
```
在处理对`bytes`数组元素的引用时，要特别小心，因为字节数组上的`.push()`可能会发生存储从短到长布局的切换

```solidity
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.12;

contract c {
  bytes public x = "012345678901234567890123456789";

  function test() public returns(uint256) {
      (x.push(), x.push()) = (0x01, 0x02);
      return x.length;
  }

  function getLength() public view returns(uint256) {
      return x.length;
  }

  // 刚开始：
  // x => 0x303132333435363738393031323334353637383930313233343536373839
  // x.length => 30
  // 执行test后
  // x => 0x30313233343536373839303132333435363738393031323334353637383900020000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000
  // x.length => 160
  // 继续执行test后
  // x => 0x303132333435363738393031323334353637383930313233343536373839000200000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000102
  // x.length => 162
}
```

这里第一个`x.push()`执行后，x仍然被存储在短的布局中，第二个`x.push()`将字节数组切换为长布局。因此，在一次赋值中最多只扩大字节数组中的一个元素。

##### 2-3、数组切片

数组切片是数组连续部分的视图，用法如`x[start:end]`，`start` 和 `end` 是 `uint256` 类型（或结果是`uint256`的表达式）。`x[start:end]`的第一个元素是`x[start]`，最后一个元素是`x[end - 1]`。

如果`start` 比 `end` 大，或者 `end` 比数组长度大，将会抛出异常。

`start` 和 `end` 都是可选的，`start` 默认是 0， `end` 默认是数组长度。

> 数组切片没有任何成员。他们可以隐式转换为其背后的类型，并支持索引访问。索引访问也是相对于切片的开始位置。数组切片没有类型名称，这意味着没有变量可以将数组切片作为类型。他们仅存在于中间表达式中。
> **目前数组切片，仅可用于 `calldata` 数组**

```solidity
function f(bytes calldata payload) public {
  // 取前四位
  bytes4 sig = bytes4(payload[:4]);
  // 由于截断行为，相当于
  // bytes4 sig = bytes4(payload);
}
```

##### 2-4、结构体

通过结构体的形式可以定义新的类型。

```solidity
//SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

// 在合约外部定义的结构体可以使其被多个合约共享
// 提供资金的人
struct Funder {
    address addr;
    uint amount;
}
// 众筹合约
contract CrowdFunding {
  // 在合约内部定义的结构体可以使其在此合约和衍生合约中可见
  // 活动
  struct Campaign {
      address payable beneficiary; // 受益人
      uint fundingGoal; // 筹集目标
      uint numFunders; // 提供资金的人数
      uint amount; // 实际金额
      mapping (uint => Funder) funders; // 提供资金的人
  }

  uint numCampaigns; // 活动数量

  mapping (uint => Campaign) campaigns;
  // 创建一个新的活动
  function newCampaign(address payable _beneficiary, uint _goal) 
      public 
      returns (uint campaignID) 
  {
      // campaignID作为一个变量返回，活动的数量加一
      campaignID = numCampaigns++;
      // 不能使用下方这个方式，因为右边会创建一个包含映射的内存结构体Campaign，映射只能是存储`storage`的数据位置
      // campaigns[campaignID] = Campaign(_beneficiary, _goal, 0, 0)
      Campaign storage c = campaigns[campaignID];
      c.beneficiary = _beneficiary;
      c.fundingGoal = _goal;
  }
  // 捐献函数
  function contribute(uint campaignID) public payable {
      // 拿到活动
      Campaign storage c = campaigns[campaignID];
      // 添加活动捐献人
      c.funders[c.numFunders++] = Funder({addr: msg.sender, amount: msg.value});
      // 添加活动的捐献金额
      c.amount += msg.value;
  }
  // 检查是否达到目标
  function checkGoalReached(uint campaignID) public returns(bool reached) {
      Campaign storage c = campaigns[campaignID];
      if(c.amount < c.fundingGoal)
          return false;
      // 达到目标，发送出去
      uint amount = c.amount;
      c.amount = 0;
      c.beneficiary.transfer(amount);
      return true;
  }
} 
```

结构体可以作为元素用在映射和数组中，其自身也可以包含映射和数组作为成员变量。在函数中使用时，一个结构体被赋值给一个存储`storage`的局部变量时，只是保存一个引用。

##### 2-5、映射

映射类型的声明方式是：`mapping (keyType => valueType)`。其中 `keyType`是：任何的内建类型，`bytes``string`或合约类型、枚举类型；`valueType`可以是包含映射类型在内的任何类型。

映射在实际的初始化过程中会创建每个可能的`key`，并将其映射到字节形式全是零的值（一个类型的默认值）。在映射中，实际上并不存储`key`，而是存储它的`keccak256`哈希值，从而方便查询实际的值。因此，映射是没有长度的，也没有`key`的集合或`value`的集合.

映射只能是存储`storage`的数据位置，因此只允许作为状态变量或函数内的存储`storage`引用或作为库函数的参数，不能用于合约公有函数的参数或返回值。这些限制同样适用于包含映射的数组或结构体。

```solidity
//SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

contract MappingExample {
  // 账户余额
  mapping (address => uint256) private _balances;
  // 其他人可以使用我的账号的多少钱
  mapping (address => mapping (address => uint256)) private _allowances;
  // 从 -》 到
  event Transfer(address indexed from, address indexed to, uint256 value);
  // 拥有者 -》 接收者
  event Approval(address indexed owner, address indexed spender, uint256 value);

  function allowance(address owner, address spender) public view returns (uint256) {
      // 返回用钱的人可以使用我的账号的多少钱
      return _allowances[owner][spender];
  }

  function transferFrom(address sender, address recipient, uint256 amount) public returns (bool) {
      // 当前调用的人可以使用发送者的钱的数量必须 >= 需要使用的数量
      require(_allowances[sender][msg.sender] >= amount, "ERC20: Allowance not high enough");
      // 转移钱
      _allowances[sender][msg.sender] -= amount;
      _transfer(sender, recipient, amount);
      return true;
  } 

  function approve(address spender, uint256 amount) public returns (bool) {
      require(spender != address(0), "ERC20: approve to the zero address");
      // 当前调用人给用钱的人提供钱
      _allowances[msg.sender][spender] = amount;
      // 触发提供事件
      emit Approval(msg.sender, spender, amount);
      return true;
  }

  function _transfer(address sender, address recipient, uint256 amount) internal {
      require(sender != address(0), "ERC20: transfer from the zero address");
      require(recipient != address(0), "ERC20: transfer to the zero address");
      // 发送的人的钱必须够
      require(_balances[sender] >= amount, "ERC20: Not enough funds");
      // 转移钱
      _balances[sender] -= amount;
      _balances[recipient] += amount;
      // 触发转移事件
      emit Transfer(sender, recipient, amount);
  }
}
```

###### 可迭代映射

映射本身是无法遍历的，即无法枚举所有的键。不过，可以在它们之上实现一个数据结构来进行迭代。

```solidity
// ./IteratorMapping文件

//SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

struct IndexValue {
    uint256 keyIndex;
    uint256 value;
}

struct KeyFlag {
    uint256 key;
    bool deleted;
}

struct itmap {
    mapping (uint256 => IndexValue) data;
    KeyFlag[] keys;
    uint256 size;
}

type Iterator is uint256;

library IteratorMapping {

  function insert(itmap storage _self, uint256 _key, uint256 _value)
      internal
      returns (bool replaced)
  {
      uint256 keyIndex = _self.data[_key].keyIndex;
      _self.data[_key].value = _value;
      if(keyIndex > 0)
          return true;
      else {
          keyIndex = _self.keys.length;

          _self.keys.push();
          _self.data[_key].keyIndex = keyIndex + 1;
          _self.keys[keyIndex].key = _key;
          _self.size++;
          return false;
      }
  }

  function remove(itmap storage _self, uint256 _key) 
      internal
      returns (bool success)
  {
      uint256 keyIndex = _self.data[_key].keyIndex;
      if(keyIndex == 0)
          return false;
      delete _self.data[_key];
      _self.keys[keyIndex - 1].deleted = true;
      _self.size--;
      return true;
  }

  function contains(itmap storage _self, uint256 _key) 
      internal
      view
      returns (bool)
  {
      return _self.data[_key].keyIndex > 0;
  }

  function iteratorStart(itmap storage _self)
      internal
      view
      returns (Iterator)
  {
      return _iteratorSkipDeleted(_self, 0);
  }

  function iteratorValid(itmap storage _self, Iterator _iterator)
      internal
      view
      returns (bool)
  {
      return Iterator.unwrap(_iterator) < _self.keys.length;
  }

  function iteratorNext(itmap storage _self, Iterator _iterator)
      internal
      view
      returns (Iterator)
  {
      return _iteratorSkipDeleted(_self, Iterator.unwrap(_iterator) + 1);
  }

  function iteratorGet(itmap storage _self, Iterator _iterator)
      internal
      view
      returns (uint256 key, uint256 value)
  {
      uint256 keyIndex = Iterator.unwrap(_iterator);
      key = _self.keys[keyIndex].key;
      value = _self.data[key].value;
  }

  function _iteratorSkipDeleted(itmap storage _self, uint256 _keyIndex)
      private
      view
      returns (Iterator)
  {
      while (_keyIndex < _self.keys.length && _self.keys[_keyIndex].deleted)
          _keyIndex++;
      return Iterator.wrap(_keyIndex);
  }
}
// ./user文件

//SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

import "./IteratorMapping.sol";

contract User {
  itmap public data;

  using IteratorMapping for itmap;

  function insert(uint256 k, uint256 v) public returns (uint256 size) {
      data.insert(k, v);
      return data.size;
  }

  function sum() public view returns (uint256 s) {
      for(
          Iterator i = data.iteratorStart();
          data.iteratorValid(i);
          i = data.iteratorNext(i)
      ) {
          (,uint256 value) = data.iteratorGet(i);
          s += value;
      }
  }
}

```

#### 3、操作符

即使两个操作数的类型不一样，也可以进行算数和位操作运算。在这些情况下，将使用以下机制来确定运算结果的类型（这在溢出的情况下很重要）：

- 如果右操作数的类型可以隐含的转换为左操作数的类型的类型，则使用左操作数的类型。
- 如果左操作数的类型可以隐含的转换为右操作数的类型的类型，则使用右操作数的类型。
- 否则，该操作不被允许。
- 运算符`**` `<<` `>>` 使用左操作数的类型来作为结果类型

##### 3-1、三元运算符

`表达式 ? 真表达式 : 假表达式`。三元运算符的结果不会为有理数类型。结果类型是由两个操作数的类型决定的，如果需要的话，首先会转换为他们的最小可容纳类型。

`255 + (true ? 1 : 0)`：将由于算数溢出而被回退。原因是`(true ? 1 : 0)`是`uint8`类型，这使得加法也要在`uint8`中执行。256超出了这个类型所允许的范围。

`1.5 + 1.5`：是有效的，以无限精度来进行有理表达式运算。

`1.5 + (true ? 1.5 : 2.5)`：是无效的，`(true ? 1.5 : 2.5)`会进行类型转换，小数有理数转换为整数，这在目前是不被允许的。

##### 3-2、复合操作及自增自减操作

复合操作：`+=` `-=` `*=` `/=` `%=` `!=` `&=` `^=` `<<=`等
`a++ a--`：表达式返回的值等于`a`在计算之前的值
`++a --a`：表达式返回的值等于`a`在计算之后的值

##### 3-3、delete

`delete a`的结果是将`a`的初始值赋值给`a`

- 对于整型变量来说，相当于 `a = 0`
- 对于动态数组来说，相当于将其重置为数组长度为0的数组
- 对于静态数组来说，相当于将数组中的所有元素重置为初始值
- `delete a[x]`，相当于删除数组索引`x`处的元素，其他的元素和长度不变，这意味着数组中留出了一个空位
- 对于结构体来说，相当于将结构体中的所有属性重置

`delete`对整个映射是无效的（因为映射的键是任意的，通常也是未知的）。因此在删除一个结构体时，结果将重置所有的非映射属性，这个过程是递归进行的，除非都是映射。然而，单个的键及其映射的值是可以被删除的。

```solidity
//SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

contract DeleteLBC {
  uint256 data;
  uint256[] dataArray;

  function f() public {
      uint256 x = data;
      delete x; // 将 x 设为 9，不影响数据
      delete data; // 将 data 设为 0，不影响x
      uint256[] storage y = dataArray; // 保存引用
      delete dataArray; // 将 dataArray.length 设为 0，y也将受到影响
      // delete y; // 这是非法的，引用了storage对象的局部变量只能由已有的storage对象赋值
  }
}
```

##### 3-3、运算符优先级

<table>
  <colgroup>
    <col width="15%" />
    <col width="35%" />
    <col width="50%" />
  </colgroup>
	<tr>
	    <th>优先级</th>
	    <th>描述</th>
	    <th>运算符</th>  
	</tr >
	<tr >
	    <td rowspan="6">1</td>
	    <td>后缀自增自减</td>
	    <td>
        <code>a++ a--</code>
      </td>
	</tr>
	<tr>
	    <td>new</td>
	    <td>
        <code>new typename</code>
      </td>
	</tr>
	<tr>
	    <td>数组</td>
	    <td>
        <code>array[index]</code>
      </td>
	</tr>
	<tr>
	    <td>结构体</td>
	    <td>
        <code>object.member</code>
      </td>
	</tr>
	<tr>
    <td>函数</td>
	  <td>
      <code>function(args...)</code>
    </td>
	</tr>
	<tr>
	    <td>括号</td>
	    <td>
        <code>(statement)</code>
      </td>
	</tr>
	<tr>
      <td rowspan="5">2</td>
	    <td>前缀自增自减</td>
	    <td>
        <code>++a --a</code>
      </td>
	</tr>
	<tr>
	    <td>一元负号</td>
	    <td>
        <code>-</code>
      </td>
	</tr>
	<tr>
	    <td >一元运算</td>
	    <td>
        <code>delete</code>
      </td>
	</tr>
	<tr>
	    <td>逻辑取反</td>
	    <td>
        <code>!</code>
      </td>
	</tr>
	<tr>
	    <td >位取反</td>
	    <td>
        <code>~</code>
      </td>
	</tr>
	<tr>
	    <td >3</td>
	    <td >幂操作</td>
	    <td >
        <code>**</code>
      </td>
	</tr>
	<tr>
	    <td >4</td>
	    <td >乘，除，取余</td>
	    <td >
        <code>* / %</code>
      </td>
	</tr>
	<tr>
	    <td >5</td>
	    <td >加，减</td>
	    <td >
        <code>+ -</code>
      </td>
	</tr>
  <tr>
	    <td >6</td>
	    <td >左移、右移</td>
	    <td >
        <code><< >></code>
      </td>
	</tr>
	<tr>
	    <td >7</td>
	    <td >位与</td>
	    <td >
        <code>&</code>
      </td>
	</tr>
	<tr>
	    <td >8</td>
	    <td >位异或</td>
	    <td >
        <code>^</code>
      </td>
	</tr>
  <tr>
	    <td >9</td>
	    <td >位或</td>
	    <td >
        <code>|</code>
      </td>
	</tr>
	<tr>
	    <td >10</td>
	    <td >比较操作符</td>
	    <td >
        <code>> >= < <=</code>
      </td>
	</tr>
	<tr>
	    <td >11</td>
	    <td >想等操作符</td>
	    <td >
        <code>== !=</code>
      </td>
	</tr>
  <tr>
	    <td >12</td>
	    <td >逻辑与</td>
	    <td >
        <code>&&</code>
      </td>
	</tr>
	<tr>
	    <td >13</td>
	    <td >逻辑或</td>
	    <td >
        <code>||</code>
      </td>
	</tr>
	<tr>
	    <td rowspan="2">14</td>
	    <td >三元运算符</td>
	    <td >
        <code>表达式?真:假</code>
      </td>
	</tr>
  <tr>
      <td >复合操作</td>
	    <td >
        <code>= += -= /= *= %= |= ^\ &= <<= >>=</code>
      </td>
	</tr>
  <tr>
	    <td >15</td>
	    <td >逗号</td>
	    <td >
        <code>,</code>
      </td>
	</tr>
</table>

#### 4、基本类型之间的转换

##### 4-1、隐式转换

在赋值，参数传递给函数以及应用运算符时，会进行隐式类型转换

##### 4-2、显式转换

如果某些情况下不支持隐式转换，可以考虑显式转换。

整数类型显式转换为更小的类型，相应的高位将被舍弃。如果转换为更大的类型，则填充左侧，转换结果还是等于原来的整数。

```solidity
uint32 a = 0x12345678;
uint16 b = uint16(a); // 0x5678

uint16 c = 0x1234;
uint32 d = uint32(c); // 0x00001234
```

定长字节数组转换为更小的字节时，会发生截断。转换为更大的字节时按正确方式填充。以固定索引访问转换后的字节和之前的值相等。

```solidity
bytes2 a = 0x1234;
bytes1 b = bytes1(a); // 0x12

bytes2 c = 0x1234;
bytes4 d = bytes4(c); // 0x12340000
```

整数和定长字节数组之间的转换

```solidity
bytes2 a = 0x1234;
uint32 b = uint32(byte4(a)); // 0x12340000
uint32 c = uint16(a); // 0x00001234
uint8 d = uint8(uint16(a)); // 0x34
uint8 e = uint8(bytes1(a)); // 0x12
```

`bytes`数组和`bytes``calldata`切片可以显式转换为固定长度的`bytes`类型。如果`bytes`数组和`bytes``calldata`切片比固定长度的`bytes`类型数组长，则在末尾发生截断，反之，末尾以零补充。

#### 5、字面常量与基本类型的转换

##### 5-1、整型与字面常量转换

十进制和十六进制字面常量可以隐式转换为任何足以表示它而不会截断的整数类型。

##### 5-2、定长字节数组和字面常量的转换

十进制字面常量不能隐式转换为定长字节数组。十六进制字面常量可以，但仅当十六进制数字大小完全符合定长字节数组长度。**零值可以转换为任何定长字节数组**。

```solidity
bytes2 a = 5432; // no
bytes2 b = 0x12; // no
bytes2 c = 0x123; // no
bytes2 d = 0x1234; // yes
bytes2 e = 0x0012; // yes
bytes2 f = 0; // yes
bytes2 g = 0x0; // yes
```

字符串字面常量和十六进制字符串字面常量可以隐式转换为定长字节数组，仅当他们的字符数与字节类型的大小相匹配。

```solidity
bytes2 a = hex"1234"; // yes
bytes2 b = "xy"; // yes
bytes2 c = hex"12"; // no
bytes2 d = hex"123"; // no
bytes2 e = "y"; // no
bytes2 f = "xyz"; // no
```

##### 5-3、地址类型

只有`bytes20`和`uint160`允许显示转换为`address`类型

从`bytes20`或其他整型显式转换为`address`类型时，都会作为`address payable`类型

一个`address a`类型可以通过`payable(a)`转换为`address payable`类型