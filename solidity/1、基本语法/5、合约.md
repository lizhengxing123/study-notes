<!--
 * @Descripttion: 
 * @Author: lizhengxing
 * @Date: 2022-10-10 14:39:46
 * @LastEditTime: 2022-10-12 22:08:31
-->

## 合约

solidity中的合约类似于面向对象语言中的类，合约中有用于数据持久化的状态变量，和可以修改状态变量的函数。调用另一个合约的函数时，会执行一个EVM函数调用，这个操作会切换执行时的上下文，这样，前一个合约的状态变量就不可以访问了。

#### 1、创建合约

可以通过以太坊交易从外部或从solidity合约内部创建合约。

创建合约时，合约的构造函数（`constructor`）会执行一次，构造函数是可选的，只允许有一个构造函数，不允许重载。

构造函数执行完毕后，合约的最终代码将部署到区块链上。此代码包括所有公共和外部函数以及所有可以通过函数调用访问的函数。部署的代码没有包括构造函数代码或构造函数调用的内部函数。

如果一个合约想要创建另一个合约，那么创建者必须知晓被创建合约的源代码（和二进制代码），这意味着不肯呢个循环创建依赖项。

```solidity
// SPDX-License-Identifier: GPL-3.0
pragma solidity >=0.4.22 <0.9.0;

contract OwnedToken {

  // TokenCreator 是后面定义的合约类型.
  // 不创建新合约的话，也可以引用它。
  TokenCreator creator;
  address owner;
  bytes32 name;

  // 这是注册 creator 和设置名称的构造函数。
  constructor(bytes32 name_) {

      // 状态变量通过其名称访问，而不是通过例如 this.owner 的方式访问。
      // 这也适用于函数，特别是在构造函数中，你只能像这样（“内部地”）调用它们，
      // 因为合约本身还不存在。
      owner = msg.sender;
      // 从 `address` 到 `TokenCreator` ，是做显式的类型转换
      // 并且假定调用合约的类型是 TokenCreator，没有真正的方法来检查这一点。
      creator = TokenCreator(msg.sender);
      name = name_;
  }

  function changeName(bytes32 newName) public {

      // 只有 creator （即创建当前合约的合约）能够更改名称 —— 因为合约是隐式转换为地址的，
      // 所以这里的比较是可行的。
      if (msg.sender == address(creator))
          name = newName;
  }

  function transfer(address newOwner) public {
      // 只有当前所有者才能发送 token。
      if (msg.sender != owner) return;
      // 我们也想询问 creator 是否可以发送。
      // 请注意，这里调用了一个下面定义的合约中的函数。
      // 如果调用失败（比如，由于 gas 不足），会立即停止执行。
      if (creator.isTokenTransferOK(owner, newOwner))
          owner = newOwner;
  }
}

contract TokenCreator {
  function createToken(bytes32 name)
  public
  returns (OwnedToken tokenAddress) {
      // 创建一个新的 Token 合约并且返回它的地址。
      // 从 JavaScript 方面来说，返回类型是简单的 `address` 类型，因为
      // 这是在 ABI 中可用的最接近的类型。
      return new OwnedToken(name);
  }

  function changeName(OwnedToken tokenAddress, bytes32 name)  public {
      // 同样，`tokenAddress` 的外部类型也是 `address` 。
      tokenAddress.changeName(name);
  }

  function isTokenTransferOK(address currentOwner, address newOwner)
      public
      view
      returns (bool ok)
  {
      // 检查一些任意的情况。
      address tokenAddress = msg.sender;
      return (keccak256(newOwner) & 0xff) == (bytes20(tokenAddress) & 0xff);
  }
}
```

#### 2、可见性和getter函数

##### 2-1、状态变量可见性

- `public`：对于`public`的状态变量，会自动生成一个`getter`函数，以便其他合约读取他们的值。当在一个合约里使用时，外部方式访问（`this.x`）会调用`getter`函数，内部访问（`x`）会直接读取存储中的值，`setter`函数不会被生成，所以其他合约也不能直接修改其值。
- `internal`：内部可见性状态变量只能在他们所定义的合约和派生合约中访问。不能被外部访问，**这是状态变量的默认可见性**。
- `private`：私有状态变量只能在他们所定义的合约中访问，派生合约中不可见。

##### 2-2、函数可见性

- `external`：外部可见性函数作为合约接口的一部分，这意味着我们可以从其他合约和交易中调用，外部函数不可以在内部调用（即`f()`不起作用，但`this.f()`可以）。
- `public`：公共函数是合约接口的一部分，可以在内部或通过消息调用。
- `internal`：内部可见性函数只能在当前合约和派生合约中访问。不能被外部访问，
- `private`：私有函数只能在当前定义他们的合约中访问，派生合约中不能访问。

##### 2-3、Getter函数

编译器会自动为所有`public`状态变量创建`getter`函数。`getter`函数具有`external`可见性。

```solidity
contract C {
  uint public num = 3;
  // getter函数为
  // function num() external view returns(uint) {
  //   return num;
  // }
  function x() public {
    num = 5; // 内部访问
    uint val = this.num(); // 外部访问
  }
  // 数组类型
  uint[] public arr;
  // getter函数为
  // function arr(uint i) external view returns(uint) {
  //   return arr[i];
  // }
  // 映射类型
  mapping (uint => address) public map;
  // getter函数为
  // function arr(uint i) external view returns(address) {
  //   return map[i];
  // }
  // 复杂类型
  struct Data {
    uint a;
    bytes3 b;
    mapping (uint => uint) map;
    uint[3] c;
    uint[] d;
    bytes e;
  }
  mapping (uint => mapping(bool => Data[])) public data;
  // getter函数
  // 结构体内的映射和数组被省略了，因为没有好办法为单个结构成员或为映射提供一个键
  function data(uint a, bool b, uint c) 
    external 
    view 
    returns (uint a, bytes3 b, bytes memory e) 
  {
    a = data[a][b][c].a;
    b = data[a][b][c].b;
    e = data[a][b][c].e;
  }
}
```

#### 3、函数修改器modifier

使用修改器`modifier`可以轻松改变函数行为，他们可以在执行函数之前自动检查某个条件。修改器`modifier`是合约的可继承属性，并可能被派生合约覆盖，但前提是他们被标注为`virtual`

```solidity
contract Owned {
  address payable owner;

  constructor() {
    owner = payable(msg.sender);
  }

  // 修改器所修饰的函数体会被插入到 _; 位置
  modifier onlyOwner() {
    require(
      msg.sender == owner,
      "Only owner can call this function."
    );
    _;
  }
}

contract Destructible is Owned {
  // 这个合约继承了onlyOwner修改器
  // 只有owner可以调用destroy函数
  function destroy() public onlyOwner {
    // owner需为payable
    selfdestruct(owner);
  }
}

contract Priced {
  // 修改器可以接收参数
  modifier costs(uint price) {
    if(msg.value >= price) {
      _;
    }
  }
}
// 继承多个合约
contract Register is Priced,Destructible {
  mapping (address => bool) registeredAddress;
  uint price;

  constructor(uint initialPrice) {
    price = initialPrice;
  }
  // 不使用payable，函数会自动拒绝所有发送给他的以太币
  function register() public payable costs(price) {
    registeredAddress[msg.sender] = true;
  }

  function changePrice(uint _price) public onlyOwner {
    price = _price;
  }
}

contract Mutex {
  bool locked;
  modifier noReentrancy() {
    require(
      !locked,
      "Reentrant call"
    );
    locked = true;
    _;
    locked = false;
  }
  // msg.sender.call(""):用给定的有效载荷（payload）发出低级 CALL 调用，
  // 返回成功状态及返回数据，发送所有可用 gas，也可以调节 gas。
  // 函数返回7后，locked = false;任然会执行
  function f() public noReentrancy returns(uint) {
    (bool success,) = msg.sender.call("");
    require(success);
    return 7;
  }
}
```

- 1、如果需要访问定义在合约`C`中的修改器`m`，只需要使用`C.m`去引用它，而不需要使用虚拟表查找。
- 2、只能使用在当前合约或基类合约中定义的修改器，修改器也可以定义在库中，但它们被限定在库函数中使用。
- 3、如果一个函数有多个修改器，它们之间以空格隔开，修改器会以此检查执行
- 4、修改器不能隐式的访问或改变它们所修饰的函数的参数或返回值。这些值只能在调用时明确的以参数传递。
- 5、在修改器中，指定何时运行被修改器应用的函数是有必要的。占位符语句用于表示被修改的函数的主体应该插在什么位置。
- 6、修改器或函数体中显式的return语句仅仅跳出当前的修改器和函数体。返回变量会被赋值，但整个执行逻辑会从前一个修改器中定义的`_;`之后继续执行。
- 7、用`return`从修改器中显式返回并不影响函数返回值。修改器可以完全不执行函数体，这种情况下，返回的变量被设置为默认值`<default value>`，就像该函数是空函数体一样。
- 8、`_;`可以在修改器中出现多次，每次都会替换为函数体。
- 9、修改器的参数可以是任意表达式，在当前上下文中，所有在函数中可见的符号，在修改器中均可见。在修改器中引入的符号在函数中不可见。

#### 4、Constant和Immutable状态变量

状态变量声明为`constant`(常量)或者`immutable`(不可变量)，在这两种情况下，合约一旦部署，变量将不再修改。

对于`constant`(常量)，它的值在编译时确定；对于`immutable`(不可变量)，它的值在部署时确定。

**与常规变量相比，常量和不可变量的gas成本低很多**。对于常量，赋值给它的表达式将复制到所有访问该常量的位置，并且每次都会对其重新求值，这样可以进行本地优化。

不可变量在执行`constructor`时对其求值，并将其值复制到代码中访问它们的所有位置。对于这些值，将保留32个字节，即使它们适合较少的字节。因此，**常量有时可能比不可变量更便宜。**

**目前仅支持字符串和值类型**

```solidity
contract C {
  uint constant X = 2**88 + 9;
  string constant TEXT = "TEXT";
  bytes32 constant MY_HASH = keccak256("abc");
  uint immutable decimals;
  uint immutable maxBalance;
  address immutable owner = msg.sender;

  constructor(uint _decimals, address _ref) {
    decimals = _decimals;
    maxBalance = _ref.balance;
  }

  function isBalanceTooHigh(address _other) public view returns(bool) {
    _other.balance > maxBalance;
  }
}
```

##### 4-1、constant

如果状态声明为`constant`(常量)，只能使用那些在编译时有确定值的表达式来给它赋值。任何通过访问`storage`、区块链数据（如`block.timestamp`,`block.number`,`address(this).balance`）或执行数据（`msg.value`,`gasleft()`）或对外部合约的调用来给它们赋值都是不可行的。

内建函数`keccak256` `sha256` `ripemd160` `ecrecover` `addmod` `mulmod` 是被允许的。

允许可能对内存分配产生副作用的表达式，它可以构建复杂的对象，如查找表（`lookup-table`）

##### 4-2、immutable

可以在函数的`constructor`中或声明时为不可变量分配任意值，只能赋值一次，且在赋值之后才可以读取。

不可变量可以在声明时赋值，不过只有在合约的构造函数执行时才被视为初始化。这意味着，你不能用一个依赖于不可变量的值在行内为另一个不可变量赋值，不过，在构造函数中可以这样做。

#### 5、函数

可以在合约内部和外部定义函数。

合约之外的函数，也称为自由函数，始终具有隐式的`internal`可见性，它们的代码包含在所有调用它们的合约中，类似于内部库函数。

```solidity
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

function sum(uint[] memory arr) pure returns (uint s) {
  for(uint i = 0; i < arr.length; i++)
    s += arr[i];
}

contract ArrayExample {
  bool found;
  function f(uint[] memory arr) public {
    uint s = sum(arr);
    require(s >= 10, "less than 10");
    found = true;
  }
}
```

自由函数不能直接访问存储变量`this`，存储和不在它们的作用域范围内的函数。

##### 5-1、函数的参数及返回值

函数参数的声明方式与变量相同，未使用的参数可以省略参数名。函数参数可以作为本地变量，也可以作为左值被赋值。

函数返回变量的声明方式与参数的声明方式相同，可以省略变量名。返回变量可以显式设置值，也可以使用`return`语句指定。

非内部函数有些类型没法返回，这些类型如下，以及它们的组合

- mapping 映射
- 内部函数类型
- 指向存储`storage`的引用类型
- 多维数组
- 结构体

> 这些限制不适用与库函数

##### 5-2、状态可变性

###### view

`view`视图函数，这种情况下要保证不修改状态。

下面的情况被认为是修改状态：

- 1、修改状态变量
- 2、产生事件
- 3、创建其他合约
- 4、使用`selfdestruct`
- 5、通过调用发送以太币
- 6、调用任何没有标记为`view`和`pure`的函数
- 7、使用低级调用（`call，delegatecall，staticcall`）
- 8、使用包含特定操作码的內联汇编

###### pure

`pure`纯函数，这种情况下，承诺不读取也不修改状态变量。它仅处理传入参数和`msg.data`，对当前区块链的状态没有任何了解。**读取一个`immutable`变量也不是一个`pure`函数**

以下被认为以读取状态：

- 1、读取状态变量
- 2、访问`address(this).balance`或者`<address>.balance`
- 3、访问`block``tx``msg`中的任何成员（**除了`msg.data`和`msg.sig`**）
- 4、调用任何未标记为`pure`的函数
- 5、使用包含某些操作码的內联汇编

纯函数能够使用`revert()``require()`在发生错误时去还原潜在的状态更改。

##### 5-3、特别的函数

###### receive接受以太函数

一个合约最多有一个`receive`函数，声明方式为`receive() external payable { ... }`。不需要`function`关键字，也没有参数和返回值，它可以是`virtual`（可以被重写），可以被重载，也可以有修改器。

在对合约没有任何附加数据调用（通常是对合约进行转账）时会执行`receive`函数。通过`.send()``.transfer()`进行交易时，如果`receive`函数不存在，但是有`payable`的`fallback`回退函数，那么会调用`fallback`回退函数。如果两个函数都没有，这个合约就没办法通过常规的转账交易接收以太（会抛出异常）。

当使用`.send()``.transfer()`进行交易时，`receive`函数只有2300gas可用，除了基础的日志输出外，进行其他操作的余地很小。下面的操作会消耗2300gas：

- 写入存储
- 创建合约
- 调用消耗大量gas的外部函数
- 发送以太币

```solidity
// 这个合约会保留所有发送给他的以太币，并没办法取回
contract Sink {
  event Received(address, uint);
  receive() external payable {
    emit Received(msg.sender, msg.value);
  }
}
```

###### fallback回退函数

合约最多有一个回退函数，声明方式为`fallback() external [payable]` 或 `fallback(bytes calldata input) external payable returns (bytes memory output)`。不需要`function`关键字，可以是`virtual`（可以被重写），可以被重载，也可以有修改器。

如果在一个合约调用中，没有其他函数与给定的函数标识符匹配，`fallback`会被调用，或者在没有`receive`函数时，而没有提供附加数据对合约调用，那么`fallback`会被执行。

`fallback`始终会接收参数，但为了同时接收以太，必须标记为`payable`。

如果使用了带参数的版本，`input`包含了发送到合约的完整数据（等于`msg.data`），并且通过`output`返回数据，返回数据不是ABI编码过的数据，而是不经过修改的数据。

回退函数在接收以太调用时，只有2300gas可用。只要有足够的gas传递给他，回退函数就可以执行复杂的操作。

```solidity
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

contract Test {
  // 如果在一个合约调用中，没有其他函数与给定的函数标识符匹配，`fallback`会被调用，
  // 发送到该合约的任何消息都会调用此函数（因为该合约没其他函数）
  // 向这个发送以太会导致异常，因为没有payable修饰符
  fallback() external {
    x = 1;
  }
  uint x;
}

contract TestPayable {
  uint x;
  uint y;
  // 除纯转账外，所有的调用都会调用这个函数
  // 任何对合约非空calldata调用都会执行回退函数
  fallback() external payable {
    x = 1;
    y = msg.value;
  }
  // 纯转账调用这个函数，例如对每个空empty calldata的调用
  receive() external payable {
    x = 2;
    y = msg.value;
  }
}

contract Caller {
  function callTest(Test test) public returns (bool) {
    (bool success,) = address(test).call(abi.encodeWithSignature("nonExistingFunction()"));
    require(success);
    // test.x 的结果变为 1
    // address(test)不允许直接调用send，因为它的回退函数没有payable
    // 转换为payable类型，才可以调用send
    address payable testPayable = payable(address(test));
    // 向test发送以太币会失败，因为fallback没有payable修饰符
    testPayable.transfer(2);
    return true;
  }

  function callTestPayable(TestPayable test) public returns (bool) {
    // 非空calldata调用使用fallback函数
    (bool success,) = address(test).call(abi.encodeWithSignature("nonExistingFunction()"));
    require(success);
    // 结果为 x == 1， y == 0
    (success,) = address(test).call{value: 20}(abi.encodeWithSignature("nonExistingFunction()"));
    require(success);
    // 结果为 x == 1， y == 20

    // 发送以太币，TestPayable的receive函数将被调用
    // 因为函数有存储写入，会比简单的send transfer 消耗更多的gas
    // 因此使用底层的call调用
    // 空calldata调用使用receive函数
    (success,) = address(test).call{value: 2 ether}("");
    require(success);
    // 结果为 x == 2， y == 2 ether
    return true;
  }
}
```

##### 5-4、函数重载

合约可以具有多个不同参数的同名函数，称为重载，也适用于继承函数。

```solidity
contract C {
  function f(uint value) public pure returns (uint out) {
    out = value;
  }

  function f(uint value, bool really) public pure returns (uint out) {
    if(really)
      out = value;
  }
}
```

###### 重载解析和参数匹配

通过将当前范围内的函数声明与函数调用中提供的参数进行匹配，可以选择重载函数。如果所有参数都可以隐式转换为预期类型，则选择函数作为重载候选项，如果一个候选都没有，则解析失败。**返回参数不作为重载解析的依据**

```solidity
contract C {
  function f(uint8 value) public pure returns (uint out) {
    out = value;
  }

  function f(uint value) public pure returns (uint out) {
    out = value;
  }

  // 调用f(50)会导致类型错误，因为50既可以隐式转换为uint8，
  // 也可以隐式转换为uint
  // 调用f(256)则会被转换为f(uint value)的重载，因为256不能转换为uint8
}
```

#### 6、时间Events

solidity事件是EVM日志功能上的抽象。应用程序可以通过以太坊客户端的RPC接口订阅和监听这些事件

事件在合约中可被继承。

事件最多三个参数可以使用`indexed`属性（是一种名为`topic`的数据结构），`topic`仅有32字节，因此如果引用类型被标记为`indexed`，则他们的`keccak256`哈希值会被作为`topic`保存。

所有非`indexed`参数是`ABI-encoded`，都将存储在日志的数据部分中。

`topic`让我们可以搜索事件，比如在为某些事件过滤一些区块，还可以按发起事件的合同地址来过滤事件。

事件签名的哈希值也是一个`topic`。如果是`anonymous`匿名事件，则无法通过名字来过滤，仅能通过合约地址来过滤。**匿名事件的优势是他们部署和调用的成本更低，`indexed`参数也可以声明4个而不是3个**

##### 6-1、事件成员

- `event.selector`：对于非匿名事件，这是一个`bytes32`值，包含事件签名的`keccak256`哈希值，在默认主题中使用。

```solidity
contract ClientReceipt {
  event Deposit {
    address indexed from; 
    bytes32 indexed id; 
    uint value; 
  }

  function deposit(bytes32 id) public payable {
    emit Deposit(msg.sender, id, msg.value);
  }
}
```

```js
// web3.js调用
var abi = /* abi由编译器产生 */;
var ClientReceipt = web3.eth.contract(abi);
var clientReceipt = ClientReceipt.at("0x123..." /* 地址 */);

var depositEvent = clientReceipt.Deposit();

// 监听变化
depositEvent.watch(function(error, result) {
  if(!error) {
    console.log(result);
  }
})
// 也可以通过传入回调函数，立即开始监听
var depositEvent = clientReceipt.Deposit(
  function(error, result) {
    if(!error) {
      console.log(result);
    }
})
```

#### 7、错误和回退语句

错误（关键字`error`）提供了一种方便且省gas的方式来向用户解释为什么一个操作会失败。他们可以被定义在合约（包括库和接口）内部和外部。

错误必须与`revert`语句一起使用，它会还原当前调用中发生的所有变化，并将错误数据传会给调用者。

```solidity
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;
/// 转账时，没有足够的余额
/// @param available 可用余额
/// @param required 请求转账金额
error InsufficientBalance(uint available, uint required);

contract TestToken {
  mapping (address => uint) balance;
  function transfer(address to, uint amount) public {
    if(amount > balance[msg.sender]) {
      revert InsufficientBalance({
          available: balance[msg.sender],
          required: amount
        }
      );
    }
    balance[msg.sender] -= amount;
    balance[to] += amount;
  }
}
```

错误不能被重写或覆盖，但是可以继承。只要作用域不同，同一个错误可以在多个地方定义。**只能使用revert语句创建错误实例**

`error.selector`包含了错误选择器的`bytes4`值

#### 8、继承

Solidity支持多重继承包括多态。

所有的函数调用都是虚拟的，这意味着最终派生的函数将被调用，除非明确给出合约名称或者使用`super`关键字。

当一个合约从多个合约继承时，在区块链上只有一个合约被创建，所有父合约的代码都将编译到子合约中。这意味着对所有父合约的内部调用也只是内部调用（`super.f()`将使用`jump`跳转而不是消息调用）。

状态变量覆盖被视为错误，派生合约（子合约）不可以再次声明与基类合约（父合约）中相同名称的可见状态变量。

```solidity
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

contract Owned {
  address payable owner;
  constructor() {
    owner = payable(msg.sender);
  }
}
// 使用is继承另一个合约，可以访问基类合约中的所有非私有成员
// 包括内部函数和状态变量
// 无法通过this来进行访问
contract Destructible is Owned {
  // virtual表示该函数在派生合约中可以重写 overriding
  function destroy() public virtual {
    if(msg.sender == owner) {
      selfdestruct(owner);
    }
  }
}
// 抽象合约仅用于给编译器提供接口
// 函数没有函数体
// 如果一个合约没有实现所有函数，则只能用作接口
abstract contract Config {
  function lookup (uint id) public virtual returns (address addr);
}

abstract contract NameReg {
  function register(bytes32 name) public virtual;
  function unregister() public virtual;
}

// 多重继承，逗号隔开
contract Named is Owned,Destructible {
  constructor(bytes32 name) {
    Config config = Config(0xD5f9D8D94886E70b06E474c3fB14Fd43E2f23970);
    NameReg(config.lookup(1)).register(name);
  }
  // 函数可以被另一个具有相同名称相同参数的函数重载
  // 如果重载函数有着不同类型输出参数，会导致错误
  // 本地和基于消息的函数调用都会考虑这些重载
  // 如果要覆盖函数，需要使用override关键字
  // 如果想再次覆盖这个函数，需要使用virtual关键字
  function destroy() public virtual override {
    if(msg.sender == owner) {
      Config config = Config(0xD5f9D8D94886E70b06E474c3fB14Fd43E2f23970);
      NameReg(config.lookup(1)).unregister();
      // 仍然可以调用特定的重载函数
      Destructible.destroy();
    }
  }
}
// 如果构造函数需要参数
// 在声明的时候需要提供
contract PriceFeed is Owned,Destructible,Named("GoldFeed") {
  uint info;
  function updateInfo(uint newInfo) public {
    if(msg.sender == owner) {
      info = newInfo;
    }
  }
  function destroy() public override(Destructible, Named) {
    Named.destroy();
  }
  function get() public view returns(uint) {
    return info;
  }
}

contract Base1 is Destructible {
  function destroy() public virtual override { /* 清除操作 1 */ super.destroy(); }
}

contract Base2 is Destructible {
  function destroy() public  virtual override { /* 清除操作 2 */ super.destroy(); }
}

contract Final is Base1, Base2 {
  function destroy() public override(Base1, Base2) { super.destroy(); }
}

// Base2调用的super函数，它不会简单在其基类合约上调用该函数，
// 相反，他在最终的继承关系图谱的下一个基类合约中调用这个函数，
// 最终的继承关系图谱为：
// Final，Base2，Base1，Destructible，Owned
// 所以，会调用Base1.destroy
```

##### 8-1、函数重写

父合约标记为`virtual`的函数可以在子合约里重写`override`，以改变他们的行为。

重写函数只能将覆盖函数的可见性从`external`改为`public`，**`private`函数不可以标记为`virtual`**

可变性根据以下顺序更改为更严格的一种：

- `nonpayable` 可以被 `view` `pure`覆盖
- `view` 可以被  `pure`覆盖
- `payable` 不能更改为任何其他可变性

```solidity
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

contract A {
  function f() external pure virtual {}
}

contract B is A {
  function f() public view override {}
}
```

对于多重继承，如果多个父合约中有相同定义的函数，`override`后面需要指定所有父合约名

```solidity

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

contract A {
  function f() public virtual {}
}
contract B {
  function f() public virtual {}
}
contract C is A,B {
  function f() public override(A,B) {}
}
```

如果合约继承自一个公共的父合约，`override`不用显式指定父合约名

```solidity
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

contract A {
  function f() public virtual {}
}
contract B is A{
  function f() public virtual {}
}
contract C is A{
  function f() public virtual {}
}
contract D is B,C{
  function f() public override {}
}
```
接口函数会自动作为`virtual`。

如果getter函数的参数和返回值与外部函数一致时，外部函数时可以被public状态变量重写。

```solidity
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

contract A {
  function f() external virtual returns(uint) { return 5; }
}
contract B is A{
  // f的getter函数的参数和返回值与外部函数一致
  // 外部函数可以被public变量重写
  // 但是public变量不能被重写
  uint public override f;
}
```

##### 8-2、修改器重写

修改器也可以被重写，类似于函数重写，均需要`virtual`和`override`关键字。

如果是多重继承，需要在`override`关键字后指定父合约名称

```solidity
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

contract A {
  modifier m() virtual {
    _;
  }
}
contract B is A{
  modifier m() override {
    _;
  }
}
contract C {
  modifier m() virtual {
    _;
  }
}
contract D is A,C{
  // 多重继承，需要指定父合约
  modifier m() override(A,C) {
    _;
  }
}
```

##### 8-3、构造函数

构造函数时使用`constructor`关键字声明的一个可选函数，它在创建合约时执行，可以在其中运行合约初始化代码。

构造函数运行后，将最终代码部署到区块链上，代码部署所需的gas与代码的长度线性相关。此代码包含所有函数部分是公有接口，以及可以通过函数调用访问的所有函数。它不包括构造函数代码，或仅从构造函数调用的内部函数。

如果没有构造函数，合约将假定采用默认构造函数，`constructor() {}`

##### 8-4、基类构造函数的参数

如果基类合约构造函数有参数，派生合约需要制定所有参数。

```solidity
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

contract A {
  uint a;
  constructor(uint _a) {
    a = _a;
  }
}
// 第一种方式
contract B is A(2) {
  constructor() {}
}
// 第二种方式
contract C is A {
  constructor(uint y) A(y * y) {
    
  }
}
// 抽象合约，无需给A提供参数
abstract contract D is A {

}

contract E is D {
  // 不是抽象合约，必须为A提供参数
  constructor() A(7 ** 2) {
    
  }
}
```

如果构造函数参数是常量并且定义或描述了合约的行为，使用第一种比较合适。如果基类构造函数的参数依赖于派生合约，应使用第二种方式。

参数只能在这两种方式中使用一种，在这两个位置都指定参数会发生错误。

**如果派生合约没有给基类合约指定参数，那么这个派生合约必须声明为抽象合约。**在这种情况下，当另一个合约从它派生出来时，另一个合约的继承列表或者构造函数必须为所有还没有指定参数的基类提供必要的参数，否则，另一个合约也必须声明为抽象合约。

##### 8-5、多重继承与线性化

基类在`is`后面的顺序按照最基类到最派生类。

当一个在不同合约中多次定义的函数被调用时，给定的基类以从右到左按照深度优先的方式进行搜索，在第一次匹配的时候停止。如果基类合约已经被搜索过，则跳过该合约。

当继承层次结构中有多个构造函数时，继承线性化特别重要，构造函数将始终以线性化顺序执行，无论在继承合约的构造函数中提供参数的顺序如何。

```solidity
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

contract A {
  uint a;
  constructor(uint _a) {
    a = _a;
  }
}

contract B {
  uint b;
  constructor(uint _b) {
    b = _b;
  }
}
// 构造函数执行顺序为
// 1 - A
// 2 - B
// 3 - C
contract C is A,B {
  constructor() A(3) B(4) {
  }
}
// 构造函数执行顺序为
// 1 - B
// 2 - A
// 3 - C
contract C is B,A {
  constructor() B(3) A(4) {
  }
}
// 构造函数执行顺序为
// 1 - B
// 2 - A
// 3 - C
// 以继承的顺序为主
contract C is B,A {
  constructor() A(3) B(4) {
  }
}
```

##### 8-6、继承有相同名字的不同类型成员

当继承时，合约中出现了相同的名字会产生错误：

- 函数和修改器重名
- 函数和事件重名
- 事件和修改器重名

**状态变量的`getter`可以覆盖`external`函数**

#### 9、抽象合约

如果未实现合约中的至少一个函数，或者合约没有为其基类合约的构造函数提供参数时，合约必须被标注为`abstract`。即使实现了所有功能，合约也可能被标注为`abstract`。抽象合约类似于接口。

```solidity
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;
// 抽象合约不能直接实例化
abstract contract A {
  // 抽象合约的函数没有具体的实现
  function f() public pure returns(uint);
}
// 如果合约继承自抽象合约
// 但如果它也没有实现未实现的函数
// 那么这个合约也要声明为抽象合约
contract B is A {
  function f() public pure returns(uint) {
    return 3;
  }
}
```

没有实现的函数和函数类型不同：

- 没有实现的函数

```solidity
function f(address addr) public pure returns(address);
```

- 函数类型(变量声明，变量的类型为函数)

```solidity
function(address addr) public pure returns(address) f;
```

#### 10、接口

接口类似于抽象合约，但它不能实现任何函数，还有进一步的限制：

- 无法继承其他合约，但可以继承其他接口
- 接口中所有的函数都需要是`external`，尽管合约中是`public`
- 无法定义构造函数
- 无法定义状态变量
- 不可以声明修改器

接口基本上仅限于合约ABI可以表示的内容，并且ABI和接口之间的转换不应该丢失任何信息。

```solidity
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

interface Token {
  enum TokenType { Fungible, NonFungible }
  struct Coin { string obverse; string reverse; }
  function transfer(address recipient, uint amount) external;
}
// 访问方式为 Token.TokenType Token.Coin
```

合约可以继承接口，接口中的函数都会隐式的标记为`virtual`，他们被重写需要`override`关键字。重写的函数被标记为`virtual`的时候，才可以被再次重写。**重写需要`override`关键字。**

```solidity
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

interface A {
  function test() external pure returns(uint);
}
interface B {
  function test() external pure returns(uint);
}
contract C is A,B {
  function test() external pure override(A,B) returns(uint) {
    return 3;
  }
}
contract D is A {
  function test() external pure override returns(uint) {
    return 4;
  }
}
```

#### 11、库

库的目的是只需要在特定的地址部署一次，他们的代码就可以通过EVM的`DELEGATECALL`特性进行重用。

如果库函数被调用，他的代码在调用合约的上下文中执行，`this` 执行调用合约。库代码访问的是调用合约的存储状态，它仅能访问调用合约明确提供的状态变量。

调用库函数与调用基类合约的函数十分类似(`L.f()`)。

使用内部调用来调用库的内部函数，这意味着所有的`internal`类型，和保存在内存中的类型都是通过引用来传递的。

```solidity
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;
// 用于保存数据，一个值是否存在
struct Data {
  mapping(uint => bool) flags;
}

Library Set {
  // 第一个参数是storage类型
  // 在调用中，参数传递的只是它的存储地址而不是内容
  // 如果该函数可以被视为对象的方法，习惯称第一个参数为self
  function inset(Data storage self, uint value) 
    public
    returns(bool)
  {
    if(self.flags[value]) {
      // 已经存在
      // 不能写入
      return false;
    }
    self.flags[value] = true;
    // 写入成功
    return true;
  }

  function remove(Data storage self, uint value) 
    public
    returns(bool)
  {
    if(!self.flags[value]) {
      // 不存在，移除失败
      return false;
    }
    // 移除这个值
    self.flags[value] = false;
    // 移除成功
    return true;
  }

  function contains(Data storage self, uint value) 
    public
    view
    returns(bool)
  {
    // 是否存在
    return self.flags[value];
  }
}

contract C {
  Data knownValues;

  function register(uint value) public {
    require(Set.insert(knownValues, value));
  }
}

// 调用Set.insert Set.remove Set.contains
// 都是外部调用
```

在库中使用内部函数和内存类型来实现自定义类型，无需支付外部函数调用产生的开销。

```solidity
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

struct bigint {
  uint[] limbs;
}

library BigInt {
  function fromUint(uint x) internal pure returns (bigint memory r) {
      r.limbs = new uint[](1);
      r.limbs[0] = x;
  }
  // 取两个uint之间的最大值
  function max(uint a, uint b) internal pure returns (uint) {
      return a > b ? a : b;
  }
  // 取a.limbs的第index值，超过a.limbs.length返回0
  function limb(bigint memory a, uint index) internal pure returns (uint) {
      return a.limbs.length > index ? a.limbs[index] : 0;
  }

  function add(bigint memory a, bigint memory b) internal pure returns (bigint memory r) {
      r.limbs = new uint[](max(a.limbs.length, b.limbs.length));
      uint carry = 0;
      for(uint i = 0; i < r.limbs.length; ++i) {
          uint limbA = limb(a, i);
          uint limbB = limb(b, i);
          unchecked {
              r.limbs[i] = limbA + limbB + carry;
              if(limbA + limbB < limbA || (limbA + limbB == type(uint).max && carry > 0)) {
                  carry = 1;
              } else {
                  carry = 0;
              }
          }
      }
      if(carry > 0) {
          // 超出范围，数组长度加1
          uint[] memory newLimbs = new uint[](r.limbs.length + 1);
          uint i;
          for(i = 0; i < r.limbs.length; ++i) {
              newLimbs[i] = r.limbs[i];
          }
          newLimbs[i] = carry;
          r.limbs = newLimbs;
      }
  } 
}
contract C {
  using BigInt for bigint;

  function f() public pure returns(uint[] memory) {
      bigint memory x = BigInt.fromUint(7);
      bigint memory y = BigInt.fromUint(type(uint).max);
      // 调用它的作为第一个参数
      // add需要两个参数
      bigint memory z = x.add(y); 
      return z.limbs; // 输出[6,1]
  }

  function c() public pure returns(uint) {
      unchecked {return type(uint).max + 7 + 0;} // 输出6
  }
}
```


与合约相比，库的限制为：

- 没有状态变量
- 不能够继承或被继承
- 不能接收以太币
- 不可以被销毁

##### 11-1、库的函数签名与选择器

尽管可以对`public`或`external`的库函数进行外部调用，但此类调用会被视为内部调用。

外部库函数比外部合约函数支持更多的参数类型，比如递归结构和指向存储的指针。

计算用于计算4字节选择器的函数签名遵循内部命名模式，以及可对合约ABI中不支持的类型的参数使用内部编码。

以下标识符可以用于函数签名中的类型：

- 值类型，非存储的`string`和`bytes`使用和合约ABI相同的标识符
- 非存储的数组类型遵循合约ABI中相同的规则
- 非存储的结构体使用完整命名引用，例如`C.s`用于`contract C { stract s {...}}`
- 存储的映射指针，`mapping(key => value) storage`
- 其他存储的指针类型使用其对应的非存储类型的类型标识符，后面附加一个空格和`storage`

除了指向存储的指针以外，参数编码与常规合约ABI相同，存储指针被编码为`uint256`值，指向它的存储插槽

选择器的函数签名为`keccak256`哈希的前四个字节组成。

```solidity
pragma solidity ^0.8.0;

library L {
  function f(uint256) external {}
}

contract C {
  function g() public pure returns (bytes4) {
      return L.f.selector; // 0xb3de648b
  }
}
```

##### 11-2、库的调用保护

如果库的代码是被`CALL`调用，而不是`DELEGATECALL`或`CALLCODE`，那么执行结果会被回退，除非是对`view`或`pure`函数的调用

#### 12、Using For

在当前合约的上下文中，使用`using L for T`，可用于附加库函数`L`给任何类型`T`作为成员函数，这些函数将接收到调用他们的对象作为第一个参数。仅在当前作用域有效。

使用`using {fa, f2, f3} for T`，表示仅有这些函数附加到类型`T`上。

使用`using L for *`，表示将库函数`L`，附加到全部类型上。

`Using For`可在文件，合约内部及合约级都是有效的。

当`Using For`在文件级别使用时，并应用于一个用户定义的类型，`global`关键字可以添加到末尾，产生的效果是，这些函数被附加到使用该类型的任何地方，而不仅仅是声明处的作用域。


```solidity
// 文件级别
// 用于保存数据，一个值是否存在
struct Data {
  mapping(uint => bool) flags;
}
using {insert, remove, contains} for Data;

// 第一个参数是storage类型
// 在调用中，参数传递的只是它的存储地址而不是内容
// 如果该函数可以被视为对象的方法，习惯称第一个参数为self
function insert(Data storage self, uint value) 
  returns(bool)
{
  if(self.flags[value]) {
    // 已经存在
    // 不能写入
    return false;
  }
  self.flags[value] = true;
  // 写入成功
  return true;
}

function remove(Data storage self, uint value) 
  returns(bool)
{
  if(!self.flags[value]) {
    // 不存在，移除失败
    return false;
  }
  // 移除这个值
  self.flags[value] = false;
  // 移除成功
  return true;
}

function contains(Data storage self, uint value) 
  view
  returns(bool)
{
  // 是否存在
  return self.flags[value];
}
```