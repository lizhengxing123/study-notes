<!--
 * @Descripttion: 
 * @Author: lizhengxing
 * @Date: 2022-10-18 21:47:31
 * @LastEditTime: 2022-10-20 21:31:55
-->

## 內联汇编

汇编是指可使用汇编器转换为机器代码的低级编程语言。汇编语言与物理机或虚拟机绑定，因为它们实现了指令集。一条指令告诉CPU执行一些基本任务。例如两数相加。

以太坊虚拟机EVM有自己的指令集，目前有[144个操作码](https://github.com/ethereum/go-ethereum/blob/15d09038a6b1d533bc78662f2f0b77ccf03aaab0/core/vm/opcodes.go#L223-L388)

EVM是基于栈的虚拟机，只允许压入或弹出数据，后进先出。栈虚拟机将所有的操作数保存在栈上。

在Solidity中使用汇编的好处：

- 细粒度控制
- 节省Gas

#### 引入汇编

使用`assembly {}`来嵌入汇编代码段，这被称为內联汇编

```solidity
function add(uint a, uint b) public pure returns (uint sum) {
    assembly {
        sum := add(a, b)
    }
}
```

> assembly 里面的代码开发语言称为Yul

汇编代码块之间不能通信，在一个汇编代码快中定义的变量，在另一个汇编代码块中不能访问。**不同的汇编代码块不共享任何名称空间**

#### 变量的定义与赋值

使用`let`来声明变量，使用`:=`来赋值。如果声明变量没有赋值，那么变量自动初始化为0值。

```solidity
assembly {
    let x // x = 0
    x := 6 // x = 6
}
```

##### 汇编中let的运行机制

`let`指令执行如下操作：

- 创建一个新的堆栈槽位
- 为变量保留此槽位
- 当代码块结束时销毁此槽位

#### 汇编中的字面量和注释

汇编中的字面量写法和`solidity`中一样，但是字符串字面量最多可以包含32个字符

```solidity
assembly {
    /*
    * 多行注释
    */
    let a := 0x12 // 16进制
    let b := 32 // 10进制
    let c := "some people" // 字符串
    // 超长字符串，会出错
    let d := "very long string more than 32 bytes" 
}
```

#### 汇编中的块和作用域

一个块的范围由一对大括号标识。变量仅在定义的块中生效。

#### 在solidity中访问变量

##### 局部变量

只需要使用变量名就可以访问局部变量。函数内的局部变量（汇编代码块外）可以在汇编代码块内访问。

##### 内存、calldata变量，状态变量

#### 汇编中的循环

##### for循环

```solidity
function forLoop(uint n, uint value) public pure returns (uint) {
    assembly {
        /*
        * let i := 0 初始化
        * lt(i, n) 判断条件，必须为函数形式
        * i := add(i, 1) 迭代后续步骤
        */
        for { let i := 0 } lt(i, n) { i := add(i, 1) } {
            value := mul(2, value)
        }
        mstore(0x0, value)
        return (0x0, 32)
    }
}
```

`for`循环中变量的作用域范围略有不同，**在初始化部分声明的变量在循环的其他部分都可以使用，在其他部分声明的变量遵循常规的作用域规则**

##### while循环

使用`for`循环来实现`while`循环，只需要留空初始化和迭代后续部分

```solidity
function whileLoop(uint n, uint value) public pure returns (uint) {
    assembly {
        let i := 0
        for {} lt(i, n) {} {
            value := mul(2, value)
            i := add(i, 1)
        }
        mstore(0x0, value)
        return (0x0, 32)
    }
}
```

#### 汇编中的判断语句

##### if

使用`if`来设置代码执行的先决条件，**没有`else`部分，必须使用大括号，即使代码只有一行**

```solidity
assembly {
    if eq(value, 0) {
        value := 1
    }
}
```

##### switch

```solidity
assembly {
    let x := 0
    switch calldataload(4)
    case 0 {
        x := calldataload(0x24)
    }
    default {
        x := calldataload(0x44)
    }
    sstore(0, div(x, 2))
}
```

语法上的限制：

- 分支列表不需要大括号，但分支的代码块需要大括号
- 所有分支条件值必须具有相同的类型且是不同的值
- 如果分支条件已经涵盖所有的值，那么不允许出现`default`条件
- 当比较的值与一个分支匹配时，控制流将停止，不会延续到下一个`case`

#### 汇编中的函数

```solidity
assembly {
    // 分配指定长度的内存，并返回内存指针
    // 不需要指定函数的可见性
    // 不需要指定参数的类型
    // 返回值使用 -> ，也不需要指定返回值的类型
    // 不需要显式返回语句，只需要给返回值赋值
    function allocate(length) -> pos {
        pos := mload(0x40)
        mstore(0x40, add(pos, length))
    }
    let pointer := allocate(64)
}
```

汇编函数运行如下：

- 从栈顶提取参数
- 将结果压入栈

在函数内部，无法访问函数外部声明的变量

可以使用`leave`退出当前函数，只能在函数内部使用

#### 操作码和汇编

操作码主要分为以下几类：

- 算数和比较操作
- 位操作
- 密码学操作
- 环境操作码，主要是与区块链相关的信息
- 存储、内存和栈操作
- 交易与合约调用操作
- 停机操作
- 日志操作

操作码信息：

- 操作码始终从堆栈顶部获取参数
- 标有`-`的操作码不会将项推送到栈上，这些操作码大多返回内存中的值
- 所有其他操作码都会将项（返回值）压入到堆栈上
- 标有`F``H``B``C`的操作码，表示从相应的版本加入
- `mem[a...b]`表示从`a`开始到`b`（不包括`b`）的内存字节
- `storage[p]`表示在`p`位置的存储内容
- `pushi``Jumpdest`不能直接使用，可以通过输入以十进制或十六进制表示形式的整数常量来使用它们

#### 高级的汇编概念

##### 多个赋值

```solidity
assembly {
    function f() -> a,b {
        // some code
    }
    let c, d := f()
}
```

##### 栈平衡

在每个`assembly { ... }`的末尾，必须平衡堆栈（除非另有要求），否则，编译器将产生警告。

#### 独立汇编

独立汇编允许使用两组操作码：`dup`和`swap`，这些操作码可以直接控制堆栈

- `dup1`到`dup16`的指令允许在堆栈上复制值
- `swap1`到`swap16`的指令允许交换堆栈上的值
- 直接堆栈分配：`3 =: x`
- 跳转操作码：`jump``jumpi`

#### 函数式风格汇编

```solidity
// 函数式
// 更容易了解哪个是操作数，哪个是操作码
mstore(0x80, add(mload(0x80), 3))

// 非函数式
// 更容易看到这些值最终在堆栈中的位置
3 0x80 mload add 0x80 mstore
```